#CATEGORY "0" // Hidden
#SYMBOL_NAME "Qsys Core"

#USER_SIMPLSHARP_LIBRARY "QscQsys"

digital_input Initialize, UseExternalConnection;

analog_input DebugMode, Port;

string_input PrimaryIpAddress[255], BackupIpAddress[255], Username[100], Password[100];

buffer_input Response[65534];

digital_output IsInitialized, PrimaryIsConnected, BackupIsConnected, PrimaryIsActive, BackupIsActive,
				IsLoggedIn, IsRedundant, IsEmulator;

string_output DesignName, Command;

string_parameter _skip_,_skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_,
Default_PrimaryIpAddress[255], Default_BackupIpAddress[255];
integer_parameter Default_Port;
string_parameter Default_Username[100], Default_Password[100], CoreID[100];

string _primaryIpa[255], _backupIpa[255], _username[100], _password[100];
integer _waitTillStart, _allowProps, _port;

QsysCore processor;

threadsafe push Initialize
{
	while(!_waitTillStart);
	
	processor.Initialize(CoreID, _primaryIpa, _backupIpa, _port, _username, _password, UseExternalConnection);
}

change DebugMode
{
	processor.Debug(DebugMode);
}

threadsafe change PrimaryIpAddress
{
	while(!_allowProps);
	
	while(_primaryIpa != PrimaryIpAddress)
	{
		_primaryIpa = PrimaryIpAddress;
		if(_waitTillStart)
		{	
			processor.PrimaryHost = _primaryIpa;
		}
	}
}

threadsafe change BackupIpAddress
{
	while(!_allowProps);
	
	while(_backupIpa != BackupIpAddress)
	{
		_backupIpa = BackupIpAddress;
		if(_waitTillStart)
		{
			processor.BackupHost = _backupIpa;
		}
	}
}

threadsafe change Username
{
	while(!_allowProps);
	
	while(_username != Username)
	{
		_username = Username;
		if(_waitTillStart)
		{
			processor.Username = _username;
		}
	}
}

threadsafe change Password
{
	while(!_allowProps);
	
	while(_password != Password)
	{
		_password = Password;
		if(_waitTillStart)
		{
			processor.Password = _password;
		}
	}
}

threadsafe change Port
{
	while(!_allowProps);
	
	while(_port != Port)
	{
		_port = Port;
		if(_waitTillStart)
		{
			processor.Port = _port;
		}
	}
}

callback function NewIsRegistered(string id, integer value)
{
	IsInitialized = value;
}

callback function NewPrimaryIsConnected(string id, integer value)
{
	PrimaryIsConnected = value;
}

callback function NewBackupIsConnected(string id, integer value)
{
	BackupIsConnected = value;
}

callback function NewCoreStatus(string id, string dName, integer redundant, integer emulator)
{
	DesignName = dName;
	IsRedundant = redundant;
	IsEmulator = emulator;
}

callback function NewIsLoggedIn(string id, integer status)
{
	IsLoggedIn = status;
}

callback function NewSendingCommand(string id, string comm)
{
	string tx[65534];
	
	tx = comm;
	Command = tx;
}

callback function NewPrimaryIsActive()
{
	BackupIsActive = OFF;
	PrimaryIsActive = ON;
}

callback function NewBackupIsActive()
{
	PrimaryIsActive = OFF;
	BackupIsActive = ON;
}

callback GatherEventHandler RxGatherHandler(GatherEventArgs e)
{
	try
	{
		if(e.Results = 0)
		{
			processor.NewExternalResponse(e.RxString);		
		}
	}
	catch
	{   
		GenerateUserError("RxGatherHandler Exception: %s\n", GetExceptionMessage());
	}
	ReArmGatherAsync(e.Input);
}

Function Main()
{    
	WaitForInitializationComplete();
	
	_port = Default_Port;
	_primaryIpa = Default_PrimaryIpAddress;
	_backupIpa = Default_BackupIpAddress;
	_username = Default_Username;
	_password = Default_Password;
	
	if(GatherAsync("\x00", Response, RxGatherHandler, 500) = -1)
	{
	}
	
	RegisterDelegate(processor, OnIsRegistered, NewIsRegistered);  
	RegisterDelegate(processor, OnPrimaryIsConnected, NewPrimaryIsConnected);
	RegisterDelegate(processor, OnBackupIsConnected, NewBackupIsConnected);
	RegisterDelegate(processor, OnNewCoreStatus, NewCoreStatus);
	RegisterDelegate(processor, OnPrimaryIsActive, NewPrimaryIsActive);
	RegisterDelegate(processor, OnBackupIsActive, NewBackupIsActive);
	RegisterDelegate(processor, OnIsLoggedIn, NewIsLoggedIn);
	RegisterDelegate(processor, OnSendingCommand, NewSendingCommand);
	
	_allowProps = 1;
	
	//do not remove delay, we need to wait until all objects are instatiated and ready
	delay(1000);
	_waitTillStart = 1;
}
